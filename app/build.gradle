apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'kotlinx-serialization'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'io.sentry.android.gradle'

apply from: "local.gradle"

android {
    compileSdk 33
    defaultConfig {
        applicationId "fr.jorisfavier.youshallnotpass"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 7
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "ANALYTIC_URL", "\"https://app.piratepx.com\""
        buildConfigField "String", "PRIVACY_POLICY_URL", "\"https://jorisfavier.fr/youshallnotpass/privacy-policy\""

    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    productFlavors {
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
    namespace 'fr.jorisfavier.youshallnotpass'
}

dependencies {
    def lifecycle_version = "2.6.1"
    def room_version = "2.5.2"
    def biometric_version = '1.1.0'
    def camerax_version = "1.2.3"
    def retrofit_version = "2.11.0"

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation 'androidx.webkit:webkit:1.7.0'
    implementation 'androidx.core:core-splashscreen:1.0.1'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    implementation "androidx.biometric:biometric:$biometric_version"
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.0"

    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    //room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"


    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.activity:activity-ktx:1.7.2'
    implementation 'androidx.fragment:fragment-ktx:1.6.2'

    implementation 'com.github.AppIntro:AppIntro:6.0.0'

    implementation 'com.jakewharton.timber:timber:5.0.1'

    //QR recognizer
    implementation 'com.google.mlkit:barcode-scanning:17.2.0'
    // CameraX core library using camera2 implementation
    implementation "androidx.camera:camera-camera2:$camerax_version"
    // CameraX Lifecycle Library
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    // CameraX View class
    implementation "androidx.camera:camera-view:$camerax_version"
    //Do not upgrade before this https://issuetracker.google.com/issues/164901843 got resolved
    implementation "androidx.security:security-crypto:1.1.0-alpha01"

    implementation 'androidx.autofill:autofill:1.1.0'
    implementation 'io.sentry:sentry-android:5.3.0'


    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:4.11.0"

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3"
    testImplementation "io.mockk:mockk:1.12.3"
    testImplementation 'android.arch.core:core-testing:1.1.1'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

}
kapt {
    correctErrorTypes = true
}
repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}
sentry {
    ignoredBuildTypes = ["debug"]
}
